plugins {
    id "java-library"
    id "kotlin"
}

version '0.1.0'

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.6'
    implementation 'com.github.dsrees:JavaPhoenixClient:1.0.0'
    implementation 'org.conscrypt:conscrypt-openjdk-uber:2.5.2'

    testImplementation 'junit:junit:4.13.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

tasks.register('uberJar', Jar) {
    // Create "Uber Jar" with all Kotlin Std deps in the built jar
    // https://docs.gradle.org/current/userguide/working_with_files.html#sec:creating_uber_jar_example    
    archiveClassifier = 'uber'

    // And this to avoid errors
    // https://stackoverflow.com/questions/67265308/gradle-entry-classpath-is-a-duplicate-but-no-duplicate-handling-strategy-has-b
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // This too
    // https://youtrack.jetbrains.com/issue/IDEA-147551
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA' 

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

test {
    // Useful for debugging opaque network failures
    //jvmArgs "-Djavax.net.debug=ssl,handshake"
}